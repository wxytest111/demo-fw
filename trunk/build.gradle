version = '1.0'

apply plugin: 'java'
apply plugin: 'jetty'
apply plugin: 'war'
apply plugin: 'idea'


sourceCompatibility = 1.6

sourceSets {
	selenium
}

repositories {
	mavenCentral()
}

dependencies {

	/* JDBC Driver */
	compile 'postgresql:postgresql:9.1-901.jdbc4'

    /*MyBatis*/
    compile 'org.mybatis:mybatis:3.1.1'

	/* Hibernate */
	compile 'javassist:javassist:3.11.0.GA'
	compile 'org.hibernate:hibernate-core:3.5.6-Final'
	compile 'org.hibernate:hibernate-annotations:3.5.6-Final'
	compile 'org.hibernate:hibernate-validator:4.2.0.Final'

	/* Spring */
	compile('org.springframework:spring-context:3.1.0.RELEASE') {
		exclude group: 'commons-logging', module: 'commons-logging'
	}
	compile 'org.springframework:spring-orm:3.1.0.RELEASE'
	compile 'org.springframework:spring-webmvc:3.1.0.RELEASE'
	compile 'org.springframework:spring-test:3.1.0.RELEASE'

    /* Spring security */
    compile 'org.springframework.security:spring-security-core:3.1.3.RELEASE'
    compile 'org.springframework.security:spring-security-web:3.1.3.RELEASE'
    compile 'org.springframework.security:spring-security-config:3.1.3.RELEASE'
    compile 'org.springframework.security:spring-security-taglibs:3.1.3.RELEASE'

	/* Logging */
	compile 'log4j:log4j:1.2.17'

	/* Servlet */
	providedCompile 'javax.servlet:servlet-api:2.5'
	providedCompile 'javax.servlet.jsp:jsp-api:2.1'
	compile 'javax.servlet:jstl:1.2'

	/* Test */
	testCompile 'junit:junit:4.10'
    testCompile 'postgresql:postgresql:9.1-901.jdbc4'
    testCompile 'org.seleniumhq.selenium:selenium-java:2.28.0'


    seleniumCompile 'postgresql:postgresql:9.1-901.jdbc4'
    seleniumCompile 'junit:junit:4.10'
    seleniumCompile 'org.seleniumhq.selenium:selenium-java:2.28.0'

}

task jettyDaemon(type: org.gradle.api.plugins.jetty.JettyRun) {
	daemon = true
}

task selenium(type: Test, dependsOn: jettyDaemon) {
	testClassesDir = sourceSets.selenium.output.classesDir
	classpath = sourceSets.selenium.runtimeClasspath
}

task initDb(type:Exec) {
    commandLine './db/create.sh'
}

test {
    exclude '**/functional/**'
}

test.doFirst {
    initDb.execute()
}

task unitTest(type: Test) {
    include '**/unit/**'
}

task integrationTest(type: Test) {
    include '**/integration/**'
}

task functionalTest(type: Test) {
    include '**/functional/**'
}

functionalTest.doFirst {
    jettyRun.daemon = true
    jettyRun.execute()
}

[jettyRunWar, jettyStop]*.stopPort = 8081
[jettyRunWar, jettyStop]*.stopKey = 'stopKey'

functionalTest.doLast {
    jettyStop.execute()
}

